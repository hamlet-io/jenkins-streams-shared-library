#!groovy

// Generate job to generate the plans for a release

@Library('hamlet-streams') _

pipeline {
    agent {
        label 'hamlet-latest'
    }

    options {
        durabilityHint('SURVIVABLE_NONATOMIC')
        parallelsAlwaysFailFast()
        skipDefaultCheckout()
    }

    parameters {
        string(
            name: 'ENVIRONMENT',
            defaultValue: '',
            description: 'Environment to target'
        )
        string(
            name: 'SEGMENT',
            defaultValue: '',
            description: 'Segment to plan'
        )
        string(
            name: 'DEPLOYMENT_GROUP',
            defaultValue: '',
            description: 'The group to deploy'
        )
        string(
            name: 'DEPLOYMENT_UNITS',
            defaultValue: '',
            description: 'The units to deploy'
        )
        string(
            name: 'CODE_UNITS',
            defaultValue: '',
            description: 'The code units to update'
        )
        string(
            name: 'GIT_COMMIT',
            defaultValue: '',
            description: 'The git commit to apply to the code units'
        )
        string(
            name: 'CODE_TAG',
            defaultValue: '',
            description: 'The tag to apply to the code units'
        )
        string(
            name: 'CHANNELS',
            defaultValue: '',
            description: 'The channels for messages'
        )
        booleanParam(
            name: 'TREAT_RUN_ID_DIFFERENCES_AS_SIGNIFICANT',
            defaultValue: false
        )
    }

    environment {
        ENVIRONMENT = "${params.ENVIRONMENT}"
        SEGMENT = "${params.SEGMENT}"
        DEPLOYMENT_UNITS_REGEX="^(" + "${params.DEPLOYMENT_UNITS.tokenize(', ').join('|')}" + ")\$"
        CODE_UNITS_LIST= params.CODE_UNITS.tokenize(', ').join(' ')

        BASE_PROPERTIES = env.JOB_NAME.split('/').first().toLowerCase()
    }

    stages {
        stage('Setup:') {
            steps {
                script {
                    currentBuild.displayName = "#${env.BUILD_NUMBER}-" + ((env.SEGMENT == "default") ? "${env.ENVIRONMENT}" : "${env.ENVIRONMENT}-${env.SEGMENT}")
                    currentBuild.description = "Group: ${params.DEPLOYMENT_GROUP}, DeploymentUnits: ${params.DEPLOYMENT_UNITS}, CodeUnits: ${params.CODE_UNITS}, Commit: ${params.GIT_COMMIT}, Tag: ${params.CODE_TAG}"
                    env.COMMIT_CONTEXT = "group=${params.DEPLOYMENT_GROUP},deploymentUnits=${params.DEPLOYMENT_UNITS.replace(',', ' ')},codeUnits=${params.CODE_UNITS.replace(',', ' ')},commit=${params.GIT_COMMIT},tag=${params.CODE_TAG}"
                }

                withFolderProperties{
                    script {
                        env.ACCOUNTS_CMDB_URL    = env.FOLDER_ACCOUNTS_CMDB_URL
                        env.ACCOUNTS_CMDB_BRANCH = env.FOLDER_ACCOUNTS_CMDB_BRANCH
                        env.PRODUCT_CMDB_URL     = env.FOLDER_PRODUCT_CMDB_URL
                        env.PRODUCT_CMDB_BRANCH  = env.FOLDER_PRODUCT_CMDB_BRANCH
                        env.GIT_CREDENTIALS_ID   = env.FOLDER_GIT_CREDENTIALS_ID
                    }
                }

                // Access the properties files
                loadPropertiesFiles()

                // Get deployment properties
                // TENANT and PRODUCT are expected to come from the properties
                loadProperties(env.BASE_PROPERTIES)

                // Permit segment overrides
                loadProperties(env.SEGMENT)

                // Determine the ACCOUNT
                setAccount(env.PRODUCT, env.ENVIRONMENT, env.SEGMENT)

                // Determine the plan branch
                script {
                    env.PLAN_BRANCH = "plan-${env.PRODUCT}-${currentBuild.displayName}"
                }

                // Construct the CMDB
                loadCMDB(
                    env.PRODUCT_CMDB_URL,
                    env.PRODUCT_CMDB_BRANCH,
                    env.GIT_CREDENTIALS_ID,
                    env.ACCOUNTS_CMDB_URL,
                    env.ACCOUNTS_CMDB_BRANCH,
                    env.PRODUCT
                )

                // Cloud access
                setCloudProviderCredentials(env.CLOUD_CREDENTIALS_ID)

                // hamlet version
                setHamletEngine(env.HAMLET_ENGINE_VERSION, env.HAMLET_CLI_VERSION)

                // Git config
                setGitConfiguration(env.GIT_EMAIL_DEFAULT,env.GIT_USER_DEFAULT)

                // Ensure plugins are loaded
                withCredentials([gitUsernamePassword(credentialsId: env.GIT_CREDENTIALS_ID)]) {
                    sh '''#!/bin/bash
                        hamlet setup || return $?
                    '''
                }
            }
        }

        stage('Update:') {
            when {
                equals expected: true, actual: false
            }

            steps {
                sh '''#!/bin/bash
                    for CODE_UNIT in ${CODE_UNITS_LIST}; do
                        hamlet release update-image-reference \
                            -u "${CODE_UNIT}" \
                            -r "${GIT_COMMIT}" \
                            ${CODE_TAG:+--code-tag ${CODE_TAG}} \
                        || return $?
                    done
                '''
            }

            post {
                failure {
                    notifyPlanUpdateFailure(
                        params.ENVIRONMENT,
                        params.SEGMENT,
                        params.DEPLOYMENT_GROUP,
                        params.CODE_UNITS,
                        params.GIT_COMMIT,
                        params.CODE_TAG,
                        params.CHANNELS
                    )
                }
            }
        }

        stage('Deploy:') {
            when {
                equals expected: true, actual: false
            }

            steps {
                sh '''#!/bin/bash
                    hamlet deploy run-deployments \
                        ${DEPLOYMENT_GROUP:+-l ${DEPLOYMENT_GROUP}} \
                        ${DEPLOYMENT_UNITS:+-u ${DEPLOYMENT_UNITS_REGEX}} \
                        --dryrun \
                    || return $?
                '''
            }

            post {
                failure {
                    notifyPlanDeployFailure(
                        params.ENVIRONMENT,
                        params.SEGMENT,
                        params.DEPLOYMENT_GROUP,
                        params.DEPLOYMENT_UNITS,
                        params.CHANNELS
                    )
                }
            }
        }

        stage('Commit:') {
            when {
                equals expected: true, actual: false
            }

            steps {
                withCredentials([gitUsernamePassword(credentialsId: env.GIT_CREDENTIALS_ID)]) {
                    sh '''#!/bin/bash
                        hamlet cmdb commit-changes --products \
                            --commit-type cd \
                            --commit-description "plan_${BUILD_NUMBER}" \
                            --commit-message "plan_${BUILD_NUMBER},environment=${ENVIRONMENT},segment=${SEGMENT},${COMMIT_CONTEXT}" \
                            --branch "${PLAN_BRANCH}" \
                            --push \
                        || return $?
                    '''
                }
            }
        }
    }
}
